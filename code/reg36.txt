********************************************************************
********************************************************************
********************************************************************
* IV in appendix
********************************************************************
********************************************************************
********************************************************************


********************************************************************
********************************************************************
********************************************************************
* READ DATA
********************************************************************
********************************************************************
********************************************************************
clear
u $main/data2/data_3

egen total = sum(number), by(inventor)

* INCLUDE THE FOLLOWING 6 LINES IF YOU DON'T WANT INTERPOLATED DATA
replace bea    =. if inter_bea   ==1
replace zd     =. if inter_zd    ==1
replace class  =. if inter_class ==1
replace bea    =. if inter_bea2  ==1
replace zd     =. if inter_zd2   ==1
replace class  =. if inter_class2==1



* CLUSTER SIZE 
g x   = log(Den_bea_zd    )
sort inventor year
by inventor : g  x_m1 = x[_n-1]
by inventor : g  x_m2 = x[_n-2]
by inventor : g  x_m3 = x[_n-3]
by inventor : g  x_m4 = x[_n-4]
by inventor : g  x_m5 = x[_n-5]
by inventor : g  x_p1 = x[_n+1]
by inventor : g  x_p2 = x[_n+2]
by inventor : g  x_p3 = x[_n+3]
by inventor : g  x_p4 = x[_n+4]
by inventor : g  x_p5 = x[_n+5]




drop if year  ==.
drop if zd ==.
drop if bea ==.
egen org_new2 = group(org_id) if org_id ~= ""



********************************************************************
********************************************************************
********************************************************************
********************************************************************
********************************************************************
********************************************************************

* NUMBER OF PATENTS IN A YEAR
g y = log(number)

* CITATIONS
* To avoid dropping the 0's, I add 0.00001
g y2 = log(citations+0.00001)
g y3 = log( (citations+0.00001) / number) 

* SPECIALIZATION
*g y4 = 1-general

* NUMBER OF FIRMS
g x3  = log(Den_bea_zdB) 


***********************
***********************
* DEFINE STAR INVENTORS
* top 10% = 3.25
***********************
***********************
keep if total  >=3.25



********************************************************************
********************************************************************
********************************************************************
* NEW VARIABLES
********************************************************************
********************************************************************
********************************************************************
egen cluster1               = group(bea zd)
egen cluster0               = group(bea zd year)
egen cluster2               = group(bea )
egen cluster_bea_year       = group(bea year)
egen cluster_bea_zd_year    = group(bea zd year)
egen cluster_bea_class_year = group(bea class year)
egen cluster_zd_year        = group(zd year)
egen cluster_bea_class      = group(bea class)
egen cluster_class_year     = group(class year)
egen org_new                = group(org_id)
egen cluster_org_year       = group(org_new year)
egen org_size               = count(inventor), by(org_new year)
egen lab_size              = count(inventor), by(org_new year bea)



********************************************************************
********************************************************************
********************************************************************
* REGRESSIONS
********************************************************************
********************************************************************
********************************************************************
keep if y~=. & zd ~=. & class ~=. & year ~=. & inventor ~=. & cluster1 ~=. & bea ~=.
summ y y2 y3  x 
summ number, detail
summ x Den_bea_zd , detail
tab zd





***************************************
*MODELS IN DIFFERENCES and IV
***************************************
save tmp35, replace


* Now I indetify firms that have a presence in only 1 city
sort bea year org_new2
g jjj=1 if org_new2 ~= .
collapse jjj, by(bea year org_new2)

* number of cities a firm is present in a given year
egen NN2 = sum(jjj), by(year org_new2)
* max number of cities a firm has ever been present 
egen NN3 = max(NN2), by(org_new2)
summ NN2 NN3, detail
sort bea year org_new2
save tmp89, replace


************************************

clear
u tmp35

drop if org_id == ""
sort org_new2 bea year zd
merge m:1   bea year zd org_new2 using  $main/data2/iv_data_new
drop _merge

sort bea year org_new2
merge m:1  bea year org_new2 using tmp89
drop _merge
rm tmp89.dta
rm tmp35.dta

g IV = iv8

sort inventor year
by inventor: g Dy    = y - y[_n-1]
by inventor: g Dx    = x - x[_n-1]
by inventor: g Dyear = year - year[_n-1]
by inventor: g Dbea  = 1 if bea_code == bea_code[_n-1] 
by inventor: g Dy_2    = y - y[_n-2]
by inventor: g Dx_2    = x - x[_n-2]
by inventor: g Dyear_2 = year - year[_n-2]
by inventor: g Dbea_2  = 1 if bea_code == bea_code[_n-2]
by inventor: g Dx_m1 = x[_n-1] - x[_n-2]
by inventor: g Dx_m2 = x[_n-2] - x[_n-3]
by inventor: g Dx_m3 = x[_n-3] - x[_n-2]
egen cluster10       = group(bea )


* Models in first differences should only include observations in contiguous years
keep if Dyear ==1
keep if Dy ~=. & Dx ~=. & IV ~=.


drop if NN3 >1


* OLS
eststo: reghdfe  Dy Dx, absorb(year  zd class )  vce(cluster cluster10)
eststo: reghdfe  Dy Dx, absorb(year  zd class cluster_zd_year                    )  vce(cluster cluster10)
eststo: reghdfe  Dy Dx, absorb(year  zd class cluster_zd_year cluster_class_year )  vce(cluster cluster10)
* 2SLS
eststo: ivreghdfe Dy (Dx =IV), absorb(year zd class ) cluster(cluster10)
eststo: ivreghdfe Dy (Dx =IV), absorb(year zd class cluster_zd_year ) cluster(cluster10)
eststo: ivreghdfe Dy (Dx =IV), absorb(year zd class cluster_zd_year cluster_class_year ) cluster(cluster10)

esttab using tables36/table22.tex, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
esttab using tables36/table22, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
eststo clear

* First Stage
eststo: reghdfe Dx IV, absorb(year zd class ) vce(cluster cluster10) res(v4)
test IV=0
eststo: reghdfe Dx IV, absorb(year zd class cluster_zd_year ) vce(cluster cluster10) res(v5) 
test IV=0
eststo: reghdfe Dx IV, absorb(year zd class cluster_zd_year cluster_class_year ) vce(cluster cluster10) res(v6)
test IV=0

esttab using tables36/table21.tex, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
esttab using tables36/table21, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
eststo clear


* EXOGENEITY TESTS
reghdfe  Dy Dx v4, absorb(year  zd class )  vce(cluster cluster10)
test v4
reghdfe  Dy Dx v5, absorb(year  zd class cluster_zd_year                    )  vce(cluster cluster10)
test v5
reghdfe  Dy Dx v6, absorb(year  zd class cluster_zd_year cluster_class_year )  vce(cluster cluster10)
test v6
drop v4 v5 v6



***************************************
* SAME CITY AND FIELD
egen max = max(bea), by(inventor)
egen min = min(bea), by(inventor)
keep if max == min
drop max min

egen max = max(zd), by(inventor)
egen min = min(zd), by(inventor)
keep if max == min
drop max min


* OLS
eststo: reghdfe  Dy Dx, absorb(year  zd class )  vce(cluster cluster10)
eststo: reghdfe  Dy Dx, absorb(year  zd class cluster_zd_year                    )  vce(cluster cluster10)
eststo: reghdfe  Dy Dx, absorb(year  zd class cluster_zd_year cluster_class_year )  vce(cluster cluster10)
* 2SLS
eststo: ivreghdfe Dy (Dx =IV), absorb(year zd class ) cluster(cluster10)
eststo: ivreghdfe Dy (Dx =IV), absorb(year zd class cluster_zd_year ) cluster(cluster10)
eststo: ivreghdfe Dy (Dx =IV), absorb(year zd class cluster_zd_year cluster_class_year ) cluster(cluster10)

esttab using tables36/table222.tex, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
esttab using tables36/table222, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
eststo clear

* First Stage
eststo: reghdfe Dx IV, absorb(year zd class ) vce(cluster cluster10) res(v4)
test IV=0
eststo: reghdfe Dx IV, absorb(year zd class cluster_zd_year ) vce(cluster cluster10) res(v5)
test IV=0
eststo: reghdfe Dx IV, absorb(year zd class cluster_zd_year cluster_class_year ) vce(cluster cluster10) res(v6)
test IV=0

esttab using tables36/table221.tex, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
esttab using tables36/table221, se  compress replace star(* 0.10 ** 0.05 *** 0.01)
eststo clear



* EXOGENEITY TESTS
reghdfe  Dy Dx v4, absorb(year  zd class )  vce(cluster cluster10)
test v4
reghdfe  Dy Dx v5, absorb(year  zd class cluster_zd_year                    )  vce(cluster cluster10)
test v5
reghdfe  Dy Dx v6, absorb(year  zd class cluster_zd_year cluster_class_year )  vce(cluster cluster10)
test v6



restore

